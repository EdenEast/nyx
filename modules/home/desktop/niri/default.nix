{
  config,
  hostName,
  lib,
  pkgs,
  self,
  ...
}: let
  cfg = config.myHome.desktop.niri;
in {
  options.myHome.desktop.niri = {
    enable = lib.mkEnableOption "niri desktop environment";

    laptopMonitor = lib.mkOption {
      description = "Internal laptop monitor.";
      default = null;
      type = lib.types.nullOr lib.types.str;
    };

    monitors = lib.mkOption {
      description = "List of external monitors.";
      default = [];
      type = lib.types.listOf lib.types.str;
    };
  };

  config = lib.mkIf config.myHome.desktop.niri.enable {
    home.packages = with pkgs; [
      fuzzel
      mako
      swaylock
      swayidle
      xwayland-satellite
    ];

    xdg.dataFile."niri/config.kdl".enable = false;
    xdg.configFile."niri/config.kdl".text = let
      content =
        (self.lib.importDir ./. (entries:
          lib.pipe entries [
            (lib.filterAttrs (name: _: name != "default"))
            (lib.mapAttrs (_: value: import value.path {}))
            builtins.attrValues
          ]))
        ++ cfg.monitors
        ++ lib.lists.optional (cfg.laptopMonitor != null) cfg.laptopMonitor;
    in
      # kdl
      ''
        // Niri Configuration for '${hostName}'
        // Generated by NixOS configuration

        ${lib.strings.concatStringsSep "\n" content}

        environment {
          XDG_CURRENT_DESKTOP "niri"
          TERMINAL "ghostty"
        }

        hotkey-overlay {
          skip-at-startup
        }

        animations {
          slowdown 0.25
        }

        prefer-no-csd
        screenshot-path "~/Pictures/Screenshots/Screenshot from %Y-%m-%d %H-%M-%S.png"

        include optional=true "${config.home.homeDirectory}/${config.xdg.dataFile."niri/config.kdl".target}"
      '';

    myHome.services.dms.enable = true;
  };
}
